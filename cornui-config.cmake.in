# Paths
set(CornUI_ROOT "${CMAKE_CURRENT_LIST_DIR}/..")

set(CornUI_RUNTIME_DIR "${CornUI_ROOT}/bin")
set(CornUI_INCLUDE_DIRS "${CornUI_ROOT}/include")
if(WIN32)
    set(CornUI_DYNAMIC_LIBRARY_DEBUG "${CornUI_RUNTIME_DIR}/cornui-d.dll")
    set(CornUI_DYNAMIC_LIBRARY_RELEASE "${CornUI_RUNTIME_DIR}/cornui.dll")
    set(CornUI_LIBRARIES_DEBUG "${CornUI_ROOT}/lib/cornui-d.lib")
    set(CornUI_LIBRARIES_RELEASE "${CornUI_ROOT}/lib/cornui.lib")
elseif(APPLE)
    set(CornUI_DYNAMIC_LIBRARY_DEBUG "${CornUI_RUNTIME_DIR}/libcornui-d.dylib")
    set(CornUI_DYNAMIC_LIBRARY_RELEASE "${CornUI_RUNTIME_DIR}/libcornui.dylib")
else()
    set(CornUI_DYNAMIC_LIBRARY_DEBUG "${CornUI_RUNTIME_DIR}/libcornui-d.so")
    set(CornUI_DYNAMIC_LIBRARY_RELEASE "${CornUI_RUNTIME_DIR}/libcornui.so")
endif()

# Provide these variables to the calling project
include(FindPackageHandleStandardArgs)
if (WIN32)
    find_package_handle_standard_args(CornUI DEFAULT_MSG CornUI_LIBRARIES_DEBUG CornUI_LIBRARIES_RELEASE CornUI_INCLUDE_DIRS)
else ()
    find_package_handle_standard_args(CornUI DEFAULT_MSG CornUI_INCLUDE_DIRS)
endif ()

if(CornUI_FOUND AND NOT TARGET Corn::CornUI)
    add_library(Corn::CornUI SHARED IMPORTED)
    set_target_properties(Corn::CornUI PROPERTIES
            IMPORTED_LOCATION_DEBUG "${CornUI_DYNAMIC_LIBRARY_DEBUG}"
            IMPORTED_LOCATION_RELEASE "${CornUI_DYNAMIC_LIBRARY_RELEASE}"
            INTERFACE_INCLUDE_DIRECTORIES "${CornUI_INCLUDE_DIRS}"
    )

    if(WIN32)
        set_target_properties(Corn::CornUI PROPERTIES
                IMPORTED_IMPLIB_DEBUG "${CornUI_LIBRARIES_DEBUG}"
                IMPORTED_IMPLIB_RELEASE "${CornUI_LIBRARIES_RELEASE}"
        )
    endif()
endif()
